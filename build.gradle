buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
	}
}

import static net.minecraftforge.gradle.dev.DevBasePlugin.*

apply plugin: 'fml'

ext.set("revision", getRevision())
ext.set("build", getBuildNo())
version = "1.11.1." + revision
group = "acs.tabbychat"
archivesBaseName = "TabbyChat"

minecraft {
    version = "1.7.10-7.10.0.939"
    assetDir = "eclipse/assets"
	
	replace "@@VERSION@@", project.version
}
processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
	inputs.property "revision", project.build
	
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info', 'litemod.json'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'revision':project.build
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info', 'litemod.json'
    }
}
sourceSets {
	main {
		java {exclude '**/LiteMod*', 'net/minecraft/**'}
		compileClasspath = configurations.default // Work around ForgeGradle#101
	}
}
dependencies {
	compile 'net.sf.jazzy:jazzy:0.5.2-rtext-1.4.1-2'
}
jar{
	from ( configurations.compile.collect { it.isDirectory() ? it : zipTree(it)}) { exclude 'META-INF/**' }
	appendix = minecraft.version
	exclude 'acs/tabbychat/core/LiteModTabbyChat.class', 'litemod.json'
}
String getBuildNo(){
	return runGit(project, project.getProjectDir(), "rev-list", "HEAD", "--count")
}
String getRevision(){
	String tag = runGit(project, project.getProjectDir(), "describe", "--tags", "--abbrev=0")
	return runGit(project, project.getProjectDir(), "rev-list", tag + "..HEAD", "--count")
}
/* // Disable until LiteLoader updates
apply plugin: 'liteloader' 
jarLiteloader{
	from configurations.compile.collect { it.isDirectory() ? it : project.zipTree(it)}
	appendix = minecraft.version
	exclude 'acs/tabbychat/core/TabbyChatMod.class', 'mcmod.info'
}
*/